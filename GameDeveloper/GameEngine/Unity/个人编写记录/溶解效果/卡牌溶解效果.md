## 目标
> 出牌后在怪物身上停留1s后，像被烧毁一样消失，变成光束进入弃牌堆，怪物播放受伤害动画，血条减去相应数值

- 溶解效果
管线：URP

## 如何使用
- Sprite Texture 原材质
- Tint控制色调
- Noise Texture 控制溶解效果范围的噪声图
- Dissolve Amount 控制溶解程度
- Edge Color 控制溶解边缘颜色
- Edge Width 控制溶解边缘大小

按照上述对shader参数的解释进行设置参数
通过c#脚本来控制材质参数来实现溶解动画

## 待实现
使用渐变贴图控制溶解颜色

## 如何实现的？

### 基本消融效果

#### 方案一（废案）：Shader Graph使用透明度剔除
首先使用透明材质的ui需要一定配置（我不了解），导致在game窗口无法正常显示（因为在unity 2022 版本 shader graph 还不支持canvas渲染）
其次由于透明的关系，当背景颜色变化的时候，对卡牌会造成一定影响
而且shader graphs没办法进行逻辑判断

通过采样噪声图实现随机消融区，利用透明度实现消融
当开启消融效果的时候，不断提高剔除范围
（disapper_clip数值控制消失程度，实现动画控制，这个数值为0-1，0为正常效果，1为完全消失，可以通过脚本程序来控制消失的程度）
为了不让alpha值影响原本的显示，我把alpha值映射到1-2的范围


### 方案二：使用clip剔除
如何开启shader的透明度剔除

噪声图像值    0.0                                  0.5                      0.8                  1.0
             ├──────────────────┼───────────────┼───────────┤
溶解判断         discard              _Dissolve Amout_              EdgeWidth

### 火焰效果
使用smoothstep和 Edge Witdh+Cutoff 与 噪声值计算出火焰的区间
然后将原材质与火焰颜色混合（这部分可以用一个火焰渐变来优化火焰效果）

### 注意
### 为什么边缘有点奇怪
这是因为贴图的wrap模式，导入的美术素材没有完美的切好（图片完全占据整个，有透明的外边），wrap模式将边缘拓展出去补充
解决方法：将美术素材切好（最方便正确的），或者修改wrap模式（选择你想要的）

### 为什么调整Color没用
请在material界面调整color来控制色调

## 最终方案源码
```c
Shader "UI/DissolveEffect"  
{  
    Properties  
    {  
        _MainTex ("Sprite Texture", 2D) = "white" {} // Source Image  
        _Color ("Tint", Color) = (1,1,1,1) // Image 的 Color       
        _NoiseTex ("Noise Texture", 2D) = "white" {} // 溶解用的噪声图  
        _Cutoff ("Dissolve Amount", Range(0,1)) = 0.0 // 溶解控制  
        [HDR]_EdgeColor ("Edge Color", Color) = (1,0.5,0,1) // 溶解边缘颜色  
        _EdgeWidth ("Edge Width", Range(0,0.2)) = 0.05 // 边缘宽度  
    }  
  
    SubShader  
    {  
        Tags  
        {  
            "Queue"="Transparent" "IgnoreProjector"="True" "RenderType"="Transparent"  
        }  
        Blend SrcAlpha OneMinusSrcAlpha  
        Cull Off        Lighting Off        ZWrite Off  
        Pass        {  
            CGPROGRAM  
            #pragma vertex vert  
            #pragma fragment frag  
            #include "UnityCG.cginc"  
  
            sampler2D _MainTex;  
            sampler2D _NoiseTex;  
            float4 _MainTex_ST;  
            float4 _NoiseTex_ST;  
            float4 _Color;  
            float _Cutoff;  
            float4 _EdgeColor;  
            float _EdgeWidth;  
  
            struct appdata_t  
            {  
                float4 vertex : POSITION;  
                float2 texcoord : TEXCOORD0;  
            };  
            struct v2f  
            {  
                float2 uv : TEXCOORD0;  
                float2 uvNoise : TEXCOORD1;  
                float4 vertex : SV_POSITION;  
            };  
            v2f vert(appdata_t v)  
            {                v2f o;  
                o.vertex = UnityObjectToClipPos(v.vertex);  
                o.uv = TRANSFORM_TEX(v.texcoord, _MainTex);  
                o.uvNoise = TRANSFORM_TEX(v.texcoord, _NoiseTex);  
                return o;  
            }  
            fixed4 frag(v2f i) : SV_Target  
            {  
                float2 uv = i.uv;  
  
                fixed4 texColor = tex2D(_MainTex, uv) * _Color;  
                float noise = tex2D(_NoiseTex, i.uvNoise).r +_EdgeWidth; // 这里加上 _EdgeWidth 是为了避免噪声值为 0 时，还会触发燃烧效果  
                clip(noise-_Cutoff); // 这里 clip 相当于控制是否溶解  
  
                float edge = smoothstep(_Cutoff, _Cutoff+ _EdgeWidth , noise);  
                texColor.rgb = lerp(_EdgeColor.rgb, texColor.rgb, edge);  
                texColor.a *= edge; // 让边缘透明度渐隐（可选）  
                return texColor;  
            }            ENDCG  
        }  
    }
}
```
